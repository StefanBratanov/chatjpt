plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id "com.diffplug.spotless" version "6.23.3"
    id "me.qoomon.git-versioning" version "6.4.3"
}

group = "io.github.stefanbratanov"

version = "develop"

gitVersioning.apply {
    refs {
        tag("v(?<version>.*)") {
            version = '${ref.version}'
        }
    }
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

def jacksonVersion = "2.16.1"

dependencies {
    implementation("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}")

    testImplementation("org.junit.jupiter:junit-jupiter:5.10.1")
    testImplementation("org.assertj:assertj-core:3.24.2")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "ChatJPT"
                description = "A minimalistic Java client for the OpenAI API"
                url = "https://github.com/StefanBratanov/chatjpt"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "StefanBratanov"
                        name = "Stefan Bratanov"
                        email = "stefan.bratanov93@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/StefanBratanov/chatjpt.git"
                    developerConnection = "scm:git:ssh://github.com/StefanBratanov/chatjpt.git"
                    url = "https://github.com/StefanBratanov/chatjpt"
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(System.getenv("SIGNING_KEY"), System.getenv("SIGNING_PASSWORD"))
    sign publishing.publications
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("OSSRH_USERNAME")
            password = System.getenv("OSSRH_PASSWORD")
        }
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}

test {
    useJUnitPlatform()
}